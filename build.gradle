import org.springframework.boot.gradle.run.BootRunTask

buildscript {
    ext {
        springBootVersion = '1.2.4.RELEASE'
        groovyVersion = '2.4.3'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('com.bluepapa32:gradle-watch-plugin:0.1.4')
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'com.bluepapa32.watch'

applicationDefaultJvmArgs = ["-javaagent:${System.env.JREBEL_HOME}/springloaded-1.2.3.RELEASE.jar", '-noverify']
//applicationDefaultJvmArgs = ["-javaagent:${System.env.JREBEL_HOME}/springloaded-1.2.3.RELEASE.jar", '-noverify', '-Xdebug', '-Xrunjdwp:server=y,transport=dt_socket,address=5005,suspend=y']

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

jar {
    baseName = 'MBTATimeToGo'
    //version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


run {
    main = 'net.scottpullen.Application'
}

bootRun.doFirst {
    Thread.start {
        project.tasks.watch.execute()
    }
}

bootRepackage {
    mainClass = 'net.scottpullen.Application'
}

watch {
    groovy {
        files files('src/main/groovy')
        tasks 'compileGroovy'
    }

    resources {
        files files('src/main/resources')
        tasks 'processResources'
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.3'

    compile("org.springframework.boot:spring-boot-starter-groovy-templates")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("org.springframework.boot:spring-boot-starter-mustache")
    compile('org.springframework:spring-context-support')
    compile('com.google.maps:google-maps-services:0.1.7')
    compile("com.fasterxml.jackson.core:jackson-databind")
    compile('joda-time:joda-time:2.2')
    compile('log4j:log4j:1.2.17')

    runtime('org.springframework:springloaded:1.2.3.RELEASE')

    testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4')
    testCompile('org.spockframework:spock-spring:1.0-groovy-2.4')
    testCompile('junit:junit:4.12')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

tasks.withType(BootRunTask) {
    systemProperty 'spring.profiles.active', 'dev'
}

test {
    systemProperty 'spring.profiles.active', 'test'
}